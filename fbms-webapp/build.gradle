buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

configurations {
    providedRuntime
}

dependencies {
    compile project(':fbms-core')

    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    compile "org.jasig.portal:uPortal-soffit-renderer:${uPortalVersion}"
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'

    /*
     * HSQLDB Database Driver Jar
     *
     * It's not appropriate for components like FBMS to come with database drivers baked-in.  They
     * should be added by implementors, perhaps through an overlay in uPortal-start.  Including them
     * in the Apereo release leads to version conflicts.
     *
     * But if you're developing FBMS and running it independently of the portal, you may need to
     * uncomment this dependency.
     *
     * runtime 'org.hsqldb:hsqldb:2.4.1'
     */
}

/**
 * This custom Gradle task starts the Spring Boot app with spring.jpa.hibernate.ddl-auto=create,
 * which drops and recreates the Hibernate-managed tables.
 */
task bootRunDataInit(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    doFirst() {
        main = 'org.apereo.portal.fbms.ApereoFbmsApplication' // Same as "normal"
        classpath = sourceSets.main.runtimeClasspath
        args = ['--init', '--spring.jpa.hibernate.ddl-auto=create']
    }
}

/**
 * This custom Gradle task starts the Spring Boot app and invokes the Importer, passing the location
 * 'docs/examples.'
 */
task bootRunDataImport(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    doFirst() {
        main = 'org.apereo.portal.fbms.ApereoFbmsApplication' // Same as "normal"
        classpath = sourceSets.main.runtimeClasspath
        args = ['--import', "${rootProject.projectDir}/docs/examples"]
    }
}

signArchives.dependsOn war
uploadArchives{
    repositories {
        mavenDeployer {
            pom.project {
                name 'fbms-webapp'
                packaging 'war'
                // optionally artifactId can be defined here
                description 'Form Builder Microservice (FBMS) for the uPortal ecosystem, by Apereo'
                url 'https://github.com/drewwills/fbms'

                scm {
                    connection 'scm:git:git://github.com/drewwills/fbms.git'
                    url 'https://github.com/drewwills/fbms'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        organization 'FBMS Developers'
                        organizationUrl 'https://github.com/drewwills/fbms/graphs/contributors'
                    }
                }
            }
        }
    }
}
